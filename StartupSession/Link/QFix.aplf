 {name}←target QFix(src opts);ext
 ⍝ cover for {2}⎕FIX 'file://...'
 ⍝ takes target ns as left arg

 (name ext)←1↓⎕NPARTS src
 :If ext≡2 U.Tail⍨DefaultOpts ⎕NS ⍬ ⍝ array
     name←StripCaseCode name
     src←U.GetFile src
     :Trap 0 ⍝ must give array, not fn or nothing
         name target.{⍎⍺,'←⍵'}Deserialise src
     :Else
         ⎕SIGNAL('EN' 11)('Message' 'There were errors processing the script ("cannot determine value")')
     :EndTrap
 :ElseIf 'ns' 'both'∊⍨⊂opts.watch ⍝ linked non-array
     :Trap 11 ⍝ fail
            :If src≢4⊃target U.GetLinkInfo name
            :OrIf ({9≠target.⎕NC ⍵:⍬ ⋄ 0::⍬ ⋄ ⎕SRC target⍎⍵}name)≢⊃⎕NGET src 1 ⍝
               name←2 target.⎕FIX'file://',src
            :EndIf
     :Else
         {}target.⎕FIX⍣(121=⎕DMX.ENX)⊢'file://',src ⍝ unnamed?
     :EndTrap
 :Else ⍝ unlinked non-array
     :Trap 11 ⍝ fail
         name←2 target.⎕FIX U.GetFile src
     :Else
         {}target.⎕FIX⍣(121=⎕DMX.ENX)U.GetFile src
     :EndTrap
 :EndIf
